# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alex <mail.avatar@gmail.com>

pkgname=exfat-utils
pkgver=1.3.0
pkgrel=2
pkgdesc='Utilities for exFAT file system'
url='https://github.com/relan/exfat'
arch=('loong64' 'x86_64')
license=('GPL2')
depends=('glibc' 'fuse2')
conflicts=('fuse-exfat')
provides=('fuse-exfat')
replaces=('fuse-exfat')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/relan/exfat/archive/v${pkgver}.tar.gz)
sha512sums=('e1c5b708f22e878d7521e3299866bba127145757e3620696ff11b18c8c7d4830522974e8ee4771234b70aa9718295d3433779cf34ca990e28ccc06cbf9325f7e')

prepare() {
  cd exfat-utils-${pkgver}
  autoreconf -fiv
}

build() {
  cd exfat-utils-${pkgver}
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin
  make CCFLAGS="${CFLAGS} ${CPPFLAGS} -std=c99" LINKFLAGS="${LDFLAGS}"
}

package() {
  cd exfat-utils-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 */*.8 -t "${pkgdir}/usr/share/man/man8"
}

# vim: ts=2 sw=2 et:
