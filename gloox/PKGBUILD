# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>

pkgname=gloox
pkgver=1.0.25
pkgrel=1
pkgdesc="C++ libraries for development of Jabber client/component/server"
arch=(loong64 x86_64)
url="https://camaya.net/gloox"
license=("GPL")
depends=('libidn' 'gnutls')
source=(https://camaya.net/download/gloox-$pkgver.tar.bz2
        gloox-fix-build.patch)
sha256sums=('cffe1cbd86dbe98be856024215321cd225001d1fa7d39118b99cb9453ba22754'
            '4476111313494a51f6faca57c246ad7179c4eb4c40c7c47231e8a47f32309212')

prepare() {
  cd "$srcdir"/gloox-$pkgver
  patch -p1 -i $srcdir/gloox-fix-build.patch
}

build() {
  cd "$srcdir"/gloox-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/gloox-$pkgver
  make DESTDIR="$pkgdir" install
}
