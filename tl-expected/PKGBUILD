# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Viktor Schneider <info[at]vjs[dot]io>
pkgname=tl-expected
pkgver=1.0.0
pkgrel=1
pkgdesc="C++11/14/17 std::expected with functional-style extensions"
arch=('any')
url="https://github.com/TartanLlama/expected"
license=('custom:CC0')
makedepends=('cmake>=3.14.0' 'catch2' 'git')
source=("expected-v1.0.0.tar.gz::https://github.com/TartanLlama/expected/archive/v1.0.0.tar.gz"
        expected-la64.patch)
sha256sums=('8f5124085a124113e75e3890b4e923e3a4de5b26a973b891b3deb40e19c03cee'
            'e331c2ec3dbb5c41d2c4fc08bf6f169b7fba1d726f8e5edeb8ed829e77fb6909')

prepare() {
    mkdir "expected-$pkgver/build"
    cd "expected-$pkgver"
    patch -p1 -i $srcdir/expected-la64.patch
}

build() {
    cd "expected-$pkgver/build"
    cmake -DCMAKE_INSTALL_PREFIX=/usr ..
    make
}

check() {
    cd "expected-$pkgver/build"
    ./tests
}

package() {
    cd "expected-$pkgver/build"
    make DESTDIR="$pkgdir/" install
    cd ..
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
